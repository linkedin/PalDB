apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'signing'
apply plugin: 'maven'

sourceCompatibility = 1.6
group = GROUP
version = VERSION_NAME

test {
  useTestNG()
}

sourceSets {

  perfTest {
    java.srcDir file('src/perfTest/java')
    resources.srcDir file('src/perfTest/resources')
    compileClasspath += sourceSets.main.output + test.output
    runtimeClasspath += sourceSets.main.output + test.output
  }

}

jar {
  from sourceSets.main.allSource
}

task perfTest(type: Test) {
  testClassesDir = sourceSets.perfTest.output.classesDir
  classpath = sourceSets.perfTest.runtimeClasspath
}

perfTest {
  useTestNG()

  maxHeapSize = "2g"
}

dependencies {
  compile 'org.xerial.snappy:snappy-java:1.0.5'

  testCompile 'org.testng:testng:6.8.8'
  testCompile 'commons-lang:commons-lang:2.6'

  perfTestCompile configurations.testCompile

  // Only used for benchmark
  perfTestCompile 'org.fusesource.leveldbjni:leveldbjni-all:1.8'
  perfTestCompile 'org.rocksdb:rocksdbjni:4.0'
}

javadoc {
  options.overview = "overview.html"
}

jacocoTestReport {
  reports {
    html.enabled = true
    xml.enabled = true
  }
}

if (project.hasProperty('release')) {

  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        // POM signature
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        // Target repository
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUser, password: ossrhPassword)
        }
        pom.project {
          group 'com.linkedin.paldb'
          name 'paldb'
          description 'Embeddable persistent write-once key-value store'
          packaging 'jar'
          url 'https://github.com/linkedin/PalDB'

          scm {
            connection 'scm:git:https://github.com/linkedin/PalDB.git'
            developerConnection 'scm:git@github.com:linkedin/PalDB.git'
            url 'https://github.com/linkedin/PalDB'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'mbastian'
              name 'Mathieu Bastian'
              email 'mbastian@apache.org'
            }
          }
        }
      }
    }
  }
}